class_name Main
extends Node


const MAIN_MENU_SCENE: String = "res://scenes/ui/main_menu/main_menu.tscn"

static var background_effects: Node2D
static var background_mask: Sprite2D

var player_scene: PackedScene = preload("uid://bvlv7g7jv37sh")
var dead_peers: Array[int] = []
var player_dictionary: Dictionary[int, Player]= {}
var player_name_dictionary: Dictionary[int, String] = {}

@onready var multiplayer_spawner: MultiplayerSpawner = $MultiplayerSpawner
@onready var player_spawn_position: Marker2D = $PayerSpawnPosition
@onready var enemy_manager: EnemyManager = $EnemyManager
@onready var _background_effects: Node2D = $BackgroundEffects
@onready var _background_mask: Sprite2D = %BackgroundMask
@onready var game_ui: GameUI = $GameUI
@onready var pause_menu: PauseMenu = $PauseMenu


func _ready() -> void:
	background_effects = _background_effects
	background_mask = _background_mask

	multiplayer_spawner.spawn_function = func(data: Variant) -> Variant:
		var player := player_scene.instantiate() as Player
		player.set_display_name(data.display_name)
		player.name = str(data.peer_id)
		player.input_multiplayer_authority = data.peer_id
		player.global_position = player_spawn_position.global_position

		if multiplayer.get_unique_id() == data.peer_id:
			game_ui.connect_player(player)

		if is_multiplayer_authority():
			if data.is_respawning:
				player.is_respawn = true
			player.died.connect(_on_player_died.bind(data.peer_id))

		player_dictionary[data.peer_id] = player

		return player

	peer_ready.rpc_id(1, MultiplayerConfig.display_name)
	
	pause_menu.quit_requested.connect(_on_quit_requested)
	multiplayer.server_disconnected.connect(_on_server_disconnected)

	if is_multiplayer_authority():
		enemy_manager.round_completed.connect(_on_round_completed)
		enemy_manager.game_completed.connect(_on_game_completed)
		multiplayer.peer_disconnected.connect(_on_peer_disconnected)


@rpc("any_peer", "call_local", "reliable")
func peer_ready(display_name: String) -> void:
	var sender_id := multiplayer.get_remote_sender_id()
	player_name_dictionary[sender_id] = display_name
	multiplayer_spawner.spawn({
		"peer_id": sender_id, 
		"display_name": player_name_dictionary[sender_id],
		"is_respawning": false
		})
	
	enemy_manager.sychronize(sender_id)


func respawn_dead_peers() -> void:
	var all_peers: PackedInt32Array = get_all_peers()
	for peer_id in dead_peers:
		if !all_peers.has(peer_id):
			continue
		multiplayer_spawner.spawn({
			"peer_id": peer_id,
			"display_name": player_name_dictionary[peer_id],
			"is_respawning": true
			})
	dead_peers.clear()


func end_game() -> void:
	get_tree().paused = false
	multiplayer.multiplayer_peer = OfflineMultiplayerPeer.new()
	get_tree().change_scene_to_file(MAIN_MENU_SCENE)


func check_game_over() -> void:
	var is_game_over: bool = true
	
	for peer_id in get_all_peers():
		if !dead_peers.has(peer_id):
			is_game_over = false
			break
	
	if is_game_over:
		end_game()


func get_all_peers() -> PackedInt32Array:
	var all_peers: PackedInt32Array= multiplayer.get_peers()
	all_peers.push_back(multiplayer.get_unique_id())
	return all_peers


func _on_player_died(peer_id: int) -> void:
	dead_peers.append(peer_id)
	check_game_over()


func _on_round_completed() -> void:
	respawn_dead_peers()


func _on_server_disconnected() -> void:
	end_game()


func _on_peer_disconnected(peer_id: int) -> void:
	if player_dictionary.has(peer_id):
		var player: Player = player_dictionary[peer_id]
		if is_instance_valid(player):
			player.kill()
		
		player_dictionary.erase(peer_id)


func _on_game_completed() -> void:
	end_game()


func _on_quit_requested() -> void:
	end_game()
